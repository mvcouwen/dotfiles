# Set locale variables
export LANG="en_US.UTF-8"
export LC_COLLATE="C" # Reset to default instead of LANG
export LC_NUMBERIC="C" # Reset to default instead of LANG
export LC_MONETARY="nl_BE.UTF-8"
export LC_PAPER="nl_BE.UTF-8"
export LC_TIME="en_US.UTF-8"

# aliases
source ~/.dotfiles/zsh/aliases.zsh

# enable vi-mode
bindkey -v
# make backspace behave as in vim, not vi
bindkey -v '^?' backward-delete-char

# register GPG_TTY
export GPG_TTY=$(tty)

# pure
autoload -U promptinit; promptinit
if prompt pure >/dev/null; then
    zstyle :prompt:pure:git:dirty color 242
    PURE_PROMPT_SYMBOL="%(?.%F{red}❯%f%F{yellow}❯%f%F{green}❯%f.❯❯❯)"
    PURE_PROMPT_VICMD_SYMBOL="%(?.%F{green}❮%f%F{yellow}❮%f%F{red}❮%f.❮❮❮)"
fi

# zsh-autosuggestions
setopt nobeep
autoload compinit; compinit
if command -v brew >/dev/null 2>&1 && [[ -f $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
    source $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh
    # overwrite function to only allow unambiguous completion (https://github.com/zsh-users/zsh-autosuggestions/issues/512)
    function _zsh_autosuggest_capture_postcompletion() {
        unset 'compstate[list]'
    }
    ZSH_AUTOSUGGEST_STRATEGY=(completion history)
    ZSH_AUTOSUGGEST_USE_ASYNC=true
    # make sure fzf-completion defaults to autosuggest-accept on tab
    ZSH_AUTOSUGGEST_IGNORE_WIDGETS+=(fzf-completion)
    fzf_default_completion=autosuggest-accept
fi

# fzf
if command -v brew >/dev/null 2>&1 && [[ -x $(brew --prefix)/opt/fzf/install ]]; then
    if [[ -f ~/.fzf.zsh ]]; then
        [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
    else
        $(brew --prefix)/opt/fzf/install --key-bindings --completion --no-update-rc >/dev/null 2>&1
        source ~/.fzf.zsh
    fi
fi

# zsh-system-clipboard (must be sourced after all bindings)
if [[ ! -f ${ZSH_CUSTOM:-~/.zsh}/plugins/zsh-system-clipboard/zsh-system-clipboard.zsh ]]; then
    if ! git clone https://github.com/kutsan/zsh-system-clipboard ${ZSH_CUSTOM:-~/.zsh}/plugins/zsh-system-clipboard >/dev/null 2>&1; then
        >&2 echo "Failed to install zsh-system-clipboard"
    fi
fi
if [[ -f ${ZSH_CUSTOM:-~/.zsh}/plugins/zsh-system-clipboard/zsh-system-clipboard.zsh ]]; then
    source ${ZSH_CUSTOM:-~/.zsh}/plugins/zsh-system-clipboard/zsh-system-clipboard.zsh
fi


# zsh-syntax-highlighting (must be sourced after everything else)
if command -v brew >/dev/null 2>&1 && [[ -f $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    source $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# pyenv
if command -v pyenv >/dev/null 2>&1; then
    export PYENV_ROOT="$HOME/.pyenv"
    command -v pyenv >/dev/null || export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init -)"
fi

# jenv
if command -v jenv >/dev/null 2>&1; then
    export JENV_ROOT="$HOME/.jenv"
    command -v jenv >/dev/null || export PATH="$JENV_ROOT/bin:$PATH"
    eval "$(jenv init -)"
fi

# Load local configuration
# Keep this at the bottom
if [[ -s "$HOME/.zshrc.local" ]]; then
    source "$HOME/.zshrc.local"
fi
